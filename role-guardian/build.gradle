apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

if (project.file("../gradle/signing/${PLATFORM_KEY_STORE}").exists() || properties.containsKey("jksPath")) {
    apply from: "../gradle/signing/platform_signing.gradle"
}

def secretsProp = new Properties()
if (rootProject.file("secrets.properties").exists()) {
    def secretsPropFile = rootProject.file("secrets.properties");
    secretsProp.load(new FileInputStream(secretsPropFile))
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "org.rfcx.guardian.guardian"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 37
        versionName "1.2.1"
        resConfigs "en"
        setProperty("archivesBaseName", "guardian-$versionName")
        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "rfcx"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "SSL_PASSPHRASE_STAGING", secretsProp['sslPassphraseStaging'])
        buildConfigField("String", "SSL_PASSPHRASE_PROD", secretsProp['sslPassphraseProd'])
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'org.rfcx.guardian.guardian'
}

dependencies {
    implementation project(':lib-core')
    implementation project(':lib-audio')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "com.android.support:support-core-utils:28.0.0"

    //Authentication
    implementation('com.auth0.android:auth0-guardian:1.25.0@aar') {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }

    //jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.1'
    implementation('io.jsonwebtoken:jjwt-orgjson:0.11.1') {
        exclude group: 'org.json', module: 'json' //provided by Android natively
    }
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.json:json:20210307'
}

repositories {
    mavenCentral()
    flatDir{
        dirs 'libs'
    }
}
